<!doctype html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>X-Mas Virus</title>
    <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
	<meta name="viewport" content="initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no, width=device-width">
    <link rel="stylesheet" href="main.css">
    <link href='https://fonts.googleapis.com/css?family=Sanchez' rel='stylesheet' type='text/css'>
</head>

</script>

<body>
    <div id="time" ></div>
    <div id="camera" ></div>
    <div id="intro" ></div>
    <div id="loading" >  Loading...</div>
    <div id="ready" >Ready. Click to start!</div>
    <div id="credits" ></div>
    <div id="start" ></div>
</body>

<script>
document.body.style.height = window.innerHeight + 20 + 'px';
</script>

<script type="x-shader/x-vertex" id="bkg-vs" >
precision highp float;

attribute vec3 position;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

uniform float mNear;
uniform float mFar;

varying float grad;
varying float vDepth;

void main() {

	vec4 mvPosition = modelViewMatrix * vec4( position, 1. );
	gl_Position = projectionMatrix * mvPosition;

	grad = 1. - ( position.y + 25. ) / 50.;
	vDepth = 1. - ( - mvPosition.z - mNear ) / ( mFar - mNear ); // will map near..far to 0..1

}

</script>

<script type="x-shader/x-fragment" id="bkg-fs" >
precision highp float;

varying float grad;
varying float vDepth;

uniform float drawDepth;

float rand(vec2 co){
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

void main() {

	if( drawDepth == 1. ) {
		gl_FragColor.rgb = vec3( vDepth );
	} else {
		vec3 top = vec3( 62., 29., 66. ) / 255.;
		vec3 bottom = vec3( 1., 12., 32. ) / 255.;
		gl_FragColor = vec4( mix( top, bottom, grad ) + .01 * rand( gl_FragCoord.xy ), 1. );
	}

}

</script>

<script type="x-shader/x-vertex" id="center-vs" >
precision highp float;

attribute vec3 position;
attribute vec3 normal;
attribute vec3 center;

uniform mat3 normalMatrix;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

uniform sampler2D colorsMap;

varying vec3 vU;
varying vec3 vNormal;
varying vec3 vCenter;

uniform float objectColor;
uniform float brightness;
uniform sampler2D spectrumTexture;
uniform float mNear;
uniform float mFar;

varying vec3 solidColor;
varying vec3 lineColor;
varying float vDepth;

void main() {

	vec4 mvPosition = modelViewMatrix * vec4( position, 1. );
	gl_Position = projectionMatrix * mvPosition;

	vU = mvPosition.xyz;
	vNormal = normalMatrix * normal;
	vCenter = center;

	vec2 cUv = vec2( 0., ( objectColor + .5 ) / 5. );
	vec3 nodeColor = .25 * texture2D( colorsMap, cUv ).rgb;
	vec3 wireColor = .25 * texture2D( colorsMap, cUv + vec2( 1. / 4., 0. )).rgb;
	vec3 nodeColorBright = texture2D( colorsMap, cUv + vec2( 2. / 4., 0. ) ).rgb;
	vec3 wireColorBright = texture2D( colorsMap, cUv + vec2( 3. / 4., 0. ) ).rgb;
	float spectrumBrightness = texture2D( spectrumTexture, vec2( ( objectColor + .5 ) / 5., 0. ) ).r;
	solidColor = mix( nodeColor, nodeColorBright, spectrumBrightness );
	lineColor = mix( wireColor, wireColorBright, spectrumBrightness );

	vDepth = 1. - ( - mvPosition.z - mNear ) / ( mFar - mNear ); // will map near..far to 0..1

}

</script>

<script type="x-shader/x-vertex" id="center-fs" >
#extension GL_OES_standard_derivatives : enable

precision highp float;

varying vec3 vU;
varying vec3 vNormal;
varying vec3 vCenter;

uniform float drawGlow;
uniform float drawDepth;

uniform sampler2D matCapMap;

uniform float brightness;

varying vec3 solidColor;
varying vec3 lineColor;
varying float vDepth;

float edgeFactorTri() {

	vec3 d = fwidth( vCenter.xyz );
	vec3 a3 = smoothstep( vec3( 0.0 ), d * 1., vCenter.xyz );
	return min( min( a3.x, a3.y ), a3.z );

}

void main() {

	if( drawDepth == 1. ) {

		gl_FragColor.rgb = vec3( vDepth );

	} else {

		if( drawGlow == 0. ) {

			vec3 n = normalize( vNormal );
			
			vec3 r = reflect( normalize( vU ), n );
			float m = 2.0 * sqrt( r.x * r.x + r.y * r.y + ( r.z + 1.0 ) * ( r.z + 1.0 ) );
			vec2 calculatedNormal = vec2( r.x / m + 0.5,  r.y / m + 0.5 );

			vec3 shading = texture2D( matCapMap, calculatedNormal ).rgb;
			vec3 color = solidColor * shading;
			gl_FragColor.rgb = mix( lineColor, color.rgb, edgeFactorTri() );
			gl_FragColor.a = 1.;

		} else {
			
			gl_FragColor.rgb = brightness * mix( lineColor, vec3( 0. ), edgeFactorTri() );		
			gl_FragColor.a = 1.;		
		}

	}

}

</script>

<script type="x-shader/x-vertex" id="object-vs" >
precision highp float;

attribute vec3 position;
attribute vec3 normal;
attribute vec3 center;

attribute float id;

uniform mat3 normalMatrix;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

uniform sampler2D posTexture;
uniform sampler2D rotTexture;
uniform sampler2D orbitTexture;
uniform sampler2D offsetTexture;
uniform sampler2D colorTexture;
uniform sampler2D spectrumTexture;
uniform vec2 posDimensions;

uniform float mNear;
uniform float mFar;

uniform float time;
uniform float factor;
uniform float total;
uniform float rotFactor;

varying vec3 vNormal;
varying vec3 vCenter;
varying vec3 vU;
varying float vDepth;

uniform float objectColor;
uniform float brightness;
uniform sampler2D colorsMap;

varying vec3 solidColor;
varying vec3 lineColor;

mat4 rotationMatrix(vec3 axis, float angle) {
    axis = normalize(axis);
    float s = sin(angle);
    float c = cos(angle);
    float oc = 1.0 - c;
    
    return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
                oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
                oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
                0.0,                                0.0,                                0.0,                                1.0);
}

mat3 rotationX( float angle ) {
	return mat3( 1., 0., 0., 
				 0., cos( angle ), -sin( angle ), 
				 0., sin( angle ), cos( angle ) );
}

mat3 rotationY( float angle ) {
	return mat3( cos( angle ), 0., sin( angle ),
				 0., 1., 0.,
				 -sin( angle ), 0., cos( angle ) );
}

mat3 rotationZ( float angle ) {
	return mat3( cos( angle ), -sin( angle ), 0.,
				 sin( angle ), cos( angle ), 0., 
				 0., 0., 1. );
}

float parabola( float x, float k ){

	 return pow( 4. * x * ( 1. - x ), k );
}

#define M_PI 3.1415926535897932384626433832795

void main() {

	float fId = float( id );
	float u = mod( fId, posDimensions.x ) / posDimensions.x;
	float v = floor( fId / posDimensions.x ) / posDimensions.y;
	vec2 tUV = vec2( u, v );

	float step = 1. / total;
	float f = 0.;
	if( factor < fId * step ) f = 0.;
	else if( factor > ( fId * step + 20. * step ) ) f = 1.;
	else {
		f = ( factor - ( fId * step ) ) / ( 20. * step );
	}
	f = clamp( f, 0., 1. );

	vec3 realPosition = texture2D( posTexture, tUV ).rgb;
	vec3 orbitPosition = rotationY( .2 * time ) * texture2D( orbitTexture, tUV ).rgb;
	
	vec3 offset = mix( orbitPosition, realPosition, f );
	offset += parabola( f, 2. ) * 10. * texture2D( offsetTexture, tUV ).rgb;
	vec3 rotation = texture2D( rotTexture, tUV ).rgb;
	mat3 rotx = rotationX( rotation.x );
	mat3 roty = rotationY( rotation.y + rotFactor * ( 1. - f ) );
	mat3 rotz = rotationZ( rotation.z );

	mat3 rot = rotx * roty * rotz;

	vNormal = normalMatrix * rot * normal;

	vec3 nPos = rot * position + offset;
	vec4 mvPosition = modelViewMatrix * vec4( nPos, 1. );
	gl_Position = projectionMatrix * mvPosition;

	vU = mvPosition.xyz;
	
	vec3 color = texture2D( colorTexture, tUV ).rgb;
	vDepth = 1. - ( - mvPosition.z - mNear ) / ( mFar - mNear ); // will map near..far to 0..1
	vCenter = center;

	vec2 cUv = vec2( 0., ( color.r + .5 ) / 5. );
	vec3 nodeColor = .25 * texture2D( colorsMap, cUv ).rgb;
	vec3 wireColor = .25 * texture2D( colorsMap, cUv + vec2( 1. / 4., 0. )).rgb;
	vec3 nodeColorBright = texture2D( colorsMap, cUv + vec2( 2. / 4., 0. ) ).rgb;
	vec3 wireColorBright = texture2D( colorsMap, cUv + vec2( 3. / 4., 0. ) ).rgb;
	float spectrumBrightness = texture2D( spectrumTexture, vec2( ( color.r + .5 ) / 5., 0. ) ).r;
	solidColor = mix( nodeColor, nodeColorBright, spectrumBrightness );
	lineColor = mix( wireColor, wireColorBright, spectrumBrightness );

}

</script>

<script type="x-shader/x-vertex" id="object-fs" >
#extension GL_OES_standard_derivatives : enable

precision highp float;

uniform sampler2D matCapMap;
uniform float drawGlow;
uniform float drawDepth;

varying vec3 vNormal;
varying vec3 vU;
varying float vDepth;
varying vec3 vCenter;

uniform float brightness;

varying vec3 solidColor;
varying vec3 lineColor;

float edgeFactorTri() {

	vec3 d = fwidth( vCenter.xyz );
	vec3 a3 = smoothstep( vec3( 0.0 ), d * 1., vCenter.xyz );
	return min( min( a3.x, a3.y ), a3.z );

}

void main() {

	if( drawDepth == 1. ) {

		gl_FragColor.rgb = vec3( vDepth );

	} else {

		if( drawGlow == 0. ) {

			vec3 n = normalize( vNormal );

			vec3 r = reflect( normalize( vU ), n );
			float m = 2.0 * sqrt( r.x * r.x + r.y * r.y + ( r.z + 1.0 ) * ( r.z + 1.0 ) );
			vec2 calculatedNormal = vec2( r.x / m + 0.5,  r.y / m + 0.5 );

			vec3 shading = texture2D( matCapMap, calculatedNormal ).rgb;
			vec3 color = solidColor * shading;
			gl_FragColor.rgb = mix( lineColor, color.rgb, edgeFactorTri() );
			gl_FragColor.a = 1.;

		} else {
			
			gl_FragColor.rgb = brightness * mix( lineColor, vec3( 0. ), edgeFactorTri() );		
			gl_FragColor.a = 1.;		
		}

	}

}

</script>

<script src="libs/three.min.js"></script>
<script src="libs/OrbitControls.js"></script>
<script src="libs/OBJLoader.js"></script>
<script src="libs/Maf.js"></script>
<script src="libs/isMobile.min.js"></script>
<script src="Wagner/Wagner.js"></script>
<script src="Wagner/Wagner.base.js"></script>
<script src="Wagner/ShaderLoader.js"></script>
<script src="libs/storyline.js"></script>

<script src="main.js"></script>

</html>